// LVS Rule Deck for nmoscap
LAYOUT PRECISION 1000
RESOLUTION 1

// xcalibre capacitance and resistance file
UNIT CAPACITANCE ff

MASK RESULTS DATABASE NONE //"mask.db"

TEXT DEPTH PRIMARY
PORT DEPTH PRIMARY

FLAG SKEW YES
FLAG OFFGRID YES

// Setup default for LVS
#IFDEF RC_DECK
  //MASK SVDB DIRECTORY "svdb" QUERY
#ELSE
  MASK SVDB DIRECTORY "svdb" QUERY
#ENDIF

LVS REPORT "lvs.rep"
LVS REPORT OPTION S
LVS POWER NAME POWER_NAME
LVS GROUND NAME GROUND_NAME

///LAYOUT ALLOW DUPLICATE CELL YES

LVS SPICE PREFER PINS            YES
LVS SPICE REPLICATE DEVICES      YES
LVS ABORT ON SUPPLY ERROR        NO
LVS ALL CAPACITOR PINS SWAPPABLE YES
LVS RECOGNIZE GATES              NONE
LVS IGNORE PORTS                 NO
LVS CHECK PORT NAMES             YES
LVS REDUCE PARALLEL BIPOLAR      YES
LVS REDUCE PARALLEL DIODES       YES
LVS REDUCE PARALLEL CAPACITORS   YES
LVS REDUCE PARALLEL RESISTORS    YES
LVS REDUCE SERIES RESISTORS      YES      //Smashes series resistors
LVS REDUCE SERIES CAPACITORS     YES      //Smashes series capacitors
LVS REDUCE SPLIT GATES           NO       //Smashes MOS split-gates.
#IFNDEF FILTER_DGS_TIED_MOS
LVS FILTER UNUSED OPTION AG RC RE RG
#ELSE
LVS FILTER UNUSED OPTION AB RC RE RG
#ENDIF

//Layer Defination
LAYER NW              3  //  N-Well
LAYER ODI             6  //  Active area, thin oxide for device, or interconnection
LAYER POI             17  //  Gate poly, poly Si
LAYER NP              26  //  N+ S/D Implant
LAYER CO              30  //  Contact Window
LAYER M1i             31  //  M1
LAYER DPO            318 
LAYER MAP 17 DATATYPE 1 318 //  Dummy PO
LAYER VARi            143  //  layer to form Varactor

diff = ODI INTERACT CO 
dumpo1 = POI INSIDE diff 
dumpo2 = dumpo1 NOT INTERACT CO 
realpo = POI NOT dumpo2
gate = realpo AND diff
gatenw = gate AND NW  //  gate inside nwell ( pmos gate )

//* Define device -- tndiff
nthin = diff AND NP 
tndiff1 = nthin NOT gate 
nplug1 = tndiff1 AND NW
tiodb = diff NOT tndiff1 
tiod = tiodb NOT POI
plug_sel2 = tndiff1 OR tiod 
plug_sel = plug_sel2 INTERACT CO  //  plug select layers
nplug2 = nplug1 AND plug_sel 
nplug_dmy = nplug1 NOT nplug2 
tndiff_dmy1 = tndiff1 INTERACT nplug_dmy 
vargt = gatenw AND NP //  Varactor on thin oxide
vargt_all = vargt AND VARi
tndiff_dmy = tndiff_dmy1 NOT INTERACT vargt_all
tndiff = tndiff1 NOT tndiff_dmy 

// Device defination
DEVICE nmoscap vargt vargt(PLUS) tndiff(MINUS) <nthin> [
       property lr,wr
       nr = count(vargt)
       lr = perimeter_coincide(vargt,nthin)/nr/2
       A  = area(vargt)/nr
       wr = A/lr
]

//****** connect the layers*******
CONNECT M1 tndiff BY odCont
CONNECT POI vargt
CONNECT M1 POI BY polyCont

//*****Definr connectivity/Via layer -- odCont ***
odCont = CO INTERACT ODI 

//* Define device -- M1
M1 = M1i OR CO 

//***** Define connectivity/via layer -- polyCont ****
polyCont = CO INTERACT POI 

//******* Text layer *******
TEXT LAYER 131 ATTACH 131 M1
PORT LAYER TEXT 131

